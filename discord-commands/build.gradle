plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
}

group 'xyz.cronixzero.sapota'
version '2.2.0'

repositories {
    mavenCentral()
}

dependencies {

    // JDA
    implementation("net.dv8tion:JDA:5.0.0-beta.2") {
        exclude module: 'opus-java'
    }

    // Logging
    implementation 'org.apache.logging.log4j:log4j-api:2.18.0'
    implementation 'org.apache.logging.log4j:log4j-core:2.20.0'
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from(components.java)
            pom {
                name = 'Java Common Discord CommandFramework'
                description = 'Create Commands faster and simpler than ever'
                url = 'https://github.com/CronixZero/JCDL'
                inceptionYear = '2021'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'cronixzero'
                        name = 'CronixZero'
                    }
                }
                scm {
                    connection = 'scm:https://github.com/CronixZero/JCDL.git'
                    developerConnection = 'scm:git@github.com:CronixZero/JCDL.git'
                    url = 'https://github.com/CronixZero/JCDL'
                }
            }
        }
    }
}

signing {
    required { !project.version.endsWith("-SNAPSHOT") && !project.hasProperty("skipSigning") }

    sign publishing.publications.mavenJava
    sign configurations.archives
}

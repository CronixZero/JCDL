plugins {
    id 'java'
    id 'net.kyori.blossom' version "1.3.0"
    id 'maven-publish'
    id 'signing'
}

group 'xyz.cronixzero.sapota'
version '1.1.0'

repositories {
    mavenCentral()
}

dependencies {
    implementation project(":discord-presence")
    implementation project(":discord-commands")
}

java {
    withJavadocJar()
    withSourcesJar()
}

blossom {
    replaceToken '${version}', project.getVersion()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from(components.java)
            pom {
                name = 'Java Common Discord BotDriver'
                description = 'BotDriver starts your Bot with everything you need to start shipping faster'
                url = 'https://github.com/CronixZero/JCDL'
                inceptionYear = '2021'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'cronixzero'
                        name = 'CronixZero'
                    }
                }
                scm {
                    connection = 'scm:https://github.com/CronixZero/JCDL.git'
                    developerConnection = 'scm:git@github.com:CronixZero/JCDL.git'
                    url = 'https://github.com/CronixZero/JCDL'
                }
            }
        }
    }
}

signing {
    required { !project.version.endsWith("-SNAPSHOT") && !project.hasProperty("skipSigning") }

    sign publishing.publications.mavenJava
    sign configurations.archives
}
